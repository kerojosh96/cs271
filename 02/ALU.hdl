// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // negate x and zero x
    Mux16(a=x,b=false,sel=zx,out=out1x);
    Not16(in=out1x,out=out2x);
    Mux16(a=out1x,b=out2x,sel=nx,out=x2);
     // negate y and zero y
    Mux16(a=y,b=false,sel=zy,out=out4y);
    Not16(in=out4y,out=out5y);
    Mux16(a=out4y,b=out5y,sel=ny,out=y2);
    //and or add 
    And16(a=x2,b=y2,out=Andedxy);
    Add16(a=x2,b=y2,out=Addedxy);
    Mux16(a=Andedxy,b=Addedxy,sel=f,out=xy2);
    //final part
    Not16(in=xy2,out=Notxy2);
    Mux16(a=xy2,b=Notxy2,sel=no,out=out, out[15]=ng,out[0..7]=part1,out[8..15]=part2);
    //
    Or8Way(in=part1,out=ZRP1);
    Or8Way(in=part2,out=ZRP2);
    Or(a=ZRP1,b=ZRP2,out=ZRP3);
    Not(in=ZRP3,out=zr);
    
}
